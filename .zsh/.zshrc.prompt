set_tmux_pwd () {
  PROMPT="$PROMPT"'$([ -n "$TMUX" ] && tmux setenv TMUXPWD_$(tmux display -p "#D" | tr -d %) "$PWD")'
}

## Default shell configuration
autoload colors
colors

case ${UID} in
  0)
[ -n "${REMOTEHOST}${SSH_CONNECTION}" ] &&
  PROMPT="%{${fg[cyan]}%}$(echo ${HOST%%.*} | tr '[a-z]' '[A-Z]') ${PROMPT}"
;;
*)
  # Color
  DEFAULT=$'%{\e[1;0m%}'
  RESET="%{${reset_color}%}"
  GREEN=$'%{\e[1;32m%}'
  # GREEN="%{${fg[green]}%}"
  BLUE=$'%{\e[1;35m%}'
  BLUE="%{${fg[blue]}%}"
  BLUE_BOLD="%{$fg_bold[blue]%}"
  RED="%{${fg[red]}%}"
  CYAN="%{${fg[cyan]}%}"
  CYAN_BOLD="%{${fg_bold[cyan]}%}"
  WHITE="%{${fg[white]}%}"
  YELLOW="%{${fg[yellow]}%}"
  PURPLE="%{${fg[purple]}%}"
  SEP="⮀"

  # Prompt
  setopt prompt_subst
  PROMPT='${RESET}%{$BLUE_BOLD%}${USER}@%m: %1(v|%F${CYAN}%1v%2v%f|) ${RESET}${WHITE}${RESET}${WHITE}%~${RESET}${WHITE} '$' \n'

  # Show git branch when you are in git repository# {{{
  # http://d.hatena.ne.jp/mollifier/20100906/p1
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info
  autoload -Uz is-at-least

  if is-at-least 4.3.10; then
    zstyle ':vcs_info:git:*' check-for-changes true
    zstyle ':vcs_info:git:*' stagedstr "+"
    zstyle ':vcs_info:git:*' unstagedstr "-"
    zstyle ':vcs_info:git:*' formats '(%s)-[%c%u%b]'
    zstyle ':vcs_info:git:*' actionformats '(%s)-[%c%u%b|%a]'
  fi

  _update_vcs_info_msg() {
    psvar=()
    LANG=en_US.UTF-8 vcs_info
    [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"
  }
  add-zsh-hook precmd _update_vcs_info_msg
  # }}}
  ;;
esac

# zsh の exntended_glob と HEAD^^^ を共存させる。# {{{
# http://subtech.g.hatena.ne.jp/cho45/20080617/1213629154
typeset -A abbreviations
abbreviations=(
  "HEAD^"     "HEAD\\^"
  "HEAD^^"    "HEAD\\^\\^"
  "HEAD^^^"   "HEAD\\^\\^\\^"
  "HEAD^^^^"  "HEAD\\^\\^\\^\\^\\^"
  "HEAD^^^^^" "HEAD\\^\\^\\^\\^\\^"
)

magic-abbrev-expand () {
  local MATCH
  LBUFFER=${LBUFFER%%(#m)[-_a-zA-Z0-9^]#}
  LBUFFER+=${abbreviations[$MATCH]:-$MATCH}
}

magic-abbrev-expand-and-insert () {
  magic-abbrev-expand
  zle self-insert
}

magic-abbrev-expand-and-accept () {
  magic-abbrev-expand
  zle accept-line
}

no-magic-abbrev-expand () {
  LBUFFER+=' '
}

zle -N magic-abbrev-expand
zle -N magic-abbrev-expand-and-insert
zle -N magic-abbrev-expand-and-accept
zle -N no-magic-abbrev-expand
# }}}
