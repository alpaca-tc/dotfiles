" Δ~~~~Δ
" ξ・ェ・ξ  < I am @alpaca-tc
" ξ     ξ
" ξ     ξ
" ξ     “~～~～~〜〇
" ξ                ξ
" ξ ξξ~～~~〜~ξ ξ
" ξ_ξξ_ξξ_ξξ_ξ  =з =з =з
augroup MyAutoCmd
  autocmd!
augroup END

" ----------------------------------------
" Utils {{{
function! s:current_git() "{{{
  let vital = s:get_vital()

  if empty(vital)
    return ''
  else
    return vital.import('Prelude').path2project_directory(getcwd())
  endif
endfunction"}}}
function! s:filetype() "{{{
  return get(split(&filetype, '\.'), 0, '')
endfunction"}}}
function! s:complement_delimiter_of_directory(path) "{{{
  return isdirectory(a:path) ? a:path . '/' : a:path
endfunction"}}}
function! s:on_init() "{{{
  return has('vim_starting') || !exists('s:loaded_vimrc')
endfunction"}}}
function! ToUpperCamel(string) "{{{
  let new_name = []
  for str in split(a:string, '_')
    let str = substitute(str, '^[a-z]\|_\zs[a-z]\C', '\=toupper(submatch(0))', 'g')
    call add(new_name, str)
  endfor

  return join(new_name, '')
endfunction"}}}
"}}}

" ----------------------------------------
" initialize "{{{
let g:my = {}

" user information
let g:my.conf = {
      \ 'initialize': 0
      \ }
let g:my.info = {
      \ 'author':  'Ishii Hiroyuki',
      \ 'email':   'alpaca-tc@alpaca.tc',
      \ 'github':  'alpaca-tc',
      \ 'twitter': 'alpaca_tc'
      \ }

let g:my.bin = {
      \ 'ctags' : '/usr/local/bin/ctags',
      \ 'git' : executable('hub') ? 'hub' : 'git',
      \ }

let g:my.dir = {
      \ 'dein':        expand('~/.cache/dein'),
      \ 'ctrlp':       expand('~/.vim.trash/ctrlp'),
      \ 'memolist':    expand('~/.memolist'),
      \ 'snippets':    expand('~/.vim/snippet'),
      \ 'swap_dir':    expand('~/.cache/vim/vimswap'),
      \ 'trash_dir':   expand('~/.cache/vim/'),
      \ 'viminfo':     expand('~/.cache/vim/viminfo'),
      \ 'undodir':     expand('~/.cache/vim/undodir'),
      \ 'unite':       expand('~/.cache/vim/unite'),
      \ 'vimref':      expand('~/.cache/vim/vim-ref'),
      \ 'vimfiler':    expand('~/.cache/vim/vimfiler'),
      \ 'vimshell':    expand('~/.cache/vim/vimshell'),
      \ 'neocomplete': expand('~/.cache/vim/neocomplete'),
      \ 'vim-session': expand('~/.cache/vim/vimsession'),
      \ 'evernote':    expand('~/.cache/vim/evernote'),
      \ 'trash':       expand('~/.Trash'),
      \ }

let g:my.ft = {
      \ 'ruby_files':      ['ruby', 'Gemfile', 'haml', 'eruby', 'yaml', 'ruby.rspec', 'slim'],
      \ 'js_files':        ['javascript', 'coffeescript', 'node', 'typescript', 'vue'],
      \ 'python_files':    ['python', 'python*'],
      \ 'scala_files':     ['scala'],
      \ 'sh_files':        ['sh'],
      \ 'php_files':       ['php', 'phtml'],
      \ 'c_files':         ['c', 'cpp'],
      \ 'style_files':     ['css', 'scss', 'sass'],
      \ 'markup_files':    ['html', 'haml', 'eruby', 'php', 'xhtml', 'liquid', 'slim'],
      \ 'english_files':   ['markdown', 'help', 'text'],
      \ 'program_files':   ['ruby', 'php', 'python', 'eruby', 'vim', 'javascript', 'coffee', 'scala', 'java', 'go', 'cpp', 'haml', 'rust', 'c', 'slim', 'go'],
      \ 'ignore_patterns': ['vimfiler', 'unite'],
      \ }
" }}}

" ----------------------------------------
" basic settings "{{{
set nocompatible
let &directory = g:my.dir.swap_dir
set backspace=indent,eol,start
set clipboard+=unnamed
set formatoptions+=lcqmM
set modelines=1

if !has('nvim')
  set ttymouse=xterm2
  execute 'set viminfo+=n' . g:my.dir.viminfo . '/.viminfo'
endif
" set nomore
" set nobackup
set nowritebackup
" set norestorescreen
" set noshowmode
" set viminfo='1000,<800,s300,\"1000,f1,:1000,/1000
set visualbell t_vb=
" set helplang=en
set timeout timeoutlen=400 ttimeoutlen=70 updatetime=600
set langmenu=en_us.UTF-8
language en_US.UTF-8

set undofile
let &undodir = g:my.dir.undodir

nnoremap <Space><Space>s :<C-U>source ~/.vim/vimrc<CR>
nnoremap <Space><Space>v :<C-U>tabnew ~/.vim/vimrc<CR>
"}}}

" ----------------------------------------
" dein.vim {{{
let s:dein_repo_dir = g:my.dir.dein . '/repos/github.com/Shougo/dein.vim'

" dein.vim がなければ github から落としてくる
if &runtimepath !~# '/dein.vim'
  if !isdirectory(g:my.dir.dein)
    execute '!git clone https://github.com/Shougo/dein.vim' g:my.dir.dein
  endif
  execute 'set runtimepath^=' . fnamemodify(g:my.dir.dein, ':p')
endif

let s:toml      = '~/.vim/dein/dein.toml'
let s:lazy_toml = '~/.vim/dein/dein_lazy.toml'

let s:vimproc_dir = $HOME . "/.cache/dein/repos/github.com/Shougo/vimproc"
if isdirectory(s:vimproc_dir)
  execute 'set runtimepath+=' . s:vimproc_dir
endif

execute 'set runtimepath+=' . $HOME . '/.vim'

if dein#load_state(s:toml) && dein#load_state(s:lazy_toml)
  call dein#begin(g:my.dir.dein)
  call dein#load_toml(s:toml)
  call dein#load_toml(s:lazy_toml, { 'lazy': 1 })

  call dein#end()
  call dein#save_state()
endif

" if dein#check_install()
"   call dein#install()
" endif
"}}}

" ----------------------------------------
" Editing"{{{
set autoread
set nostartofline
set hidden
set nrformats-=octal
set nrformats+=alpha
set textwidth=0
" set gdefault
" set splitright
" set splitbelow
set previewheight=10
set helpheight=14
set matchtime=1
set winheight=5

if has('multi_byte_ime')
  set iminsert=0 imsearch=0
endif

xnoremap < <gv
xnoremap m :sort<CR>
xnoremap > >gv

nnoremap <silent><Space><Space><Space>q :qall!<CR>
nnoremap <silent><Space><Space>w :wall<CR>
nnoremap <silent><Space><Space>q :qall<CR>
nnoremap <silent><Space>q :q<CR>
nnoremap <silent><Space>w :wq<CR>
nnoremap <silent><Space>s :SudoWrite %<CR>

nnoremap <silent><C-L> :call <SID>redraw_and_doautocmd()<CR>
function! s:redraw_and_doautocmd() "{{{
  silent doautocmd CursorHoldI <buffer>
  checktime
  redraw!
  checktime
endfunction"}}}

if exists(':OverCommandLine')
  nnoremap re :OverCommandLine<CR>%s!!!g<Left><Left><Left>
  xnoremap :s :OverCommandLine<CR>s!!!g<Left><Left><Left>
  xnoremap re "zy:OverCommandLine<CR>%s!<C-R>=substitute(@z, '!', '\\!', 'g')<CR>!!gI<Left><Left><Left>
else
  nnoremap re :%s!
  xnoremap :s :s!
  xnoremap re y:%s!<C-r>=substitute(@0, '!', '\\!', 'g')<CR>!!gI<Left><Left><Left>
endif
nnoremap ,f :setfiletype<Space>

function! s:define_abbreviations() "{{{
  " Abbreviations {{{
  let s:abbr_dictionary = {
        \ 'liquid,html,javascript,coffee,scss,sass,c,cpp,eruby,haml,ruby,php,ruby.spec,python,vim' : [
        \   'fix [fix] -',
        \   'review [review] -',
        \   'todo [todo] -',
        \ ],
        \ 'javascript' : [
        \   'elsif else if',
        \   'elseif else if',
        \ ],
        \ 'vim' : [
        \ ],
        \ 'yaml': [
        \   '<: << : *'
        \ ],
        \ 'ruby.rspec' : [
        \   'sh should',
        \   "reqs require 'spec_helper'",
        \   'has_many have_many',
        \   'belongs_to belong_to',
        \ ],
        \ 'ruby' : [
        \   'req require',
        \ ],
        \ 'scss': [
        \   'in include',
        \   'im import',
        \   'mi mixin',
        \ ],
        \ 'python' : [
        \   'im import',
        \ ],
        \ 'gitcommit' : [
        \   'wip [WIP]',
        \   'stash [STASH]',
        \   'imo IMO',
        \ ],
        \ }
  "}}}

  autocmd! Abbreviations
  for [filetype, abbreviation_definitions] in items(s:abbr_dictionary)
    call alpaca#initialize#define_abbreviations(abbreviation_definitions, filetype)
  endfor
endfunction

if s:on_init()
  augroup Abbreviations
    autocmd!
    autocmd FileType * call <SID>define_abbreviations()
  augroup END
endif
"}}}

cnoremap <C-L> <Right>

xnoremap H <Nop>
inoremap <C-@> <Nop>

augroup MyEditing
  autocmd!
  autocmd BufEnter * execute ':silent! lcd! ' . fnameescape(expand('%:p:h'))
  autocmd Filetype xml,html,eruby inoremap <buffer> </ </<C-X><C-O>
  " autocmd FileType slim,ruby,eruby,haml,racc,ruby.racc inoremap <buffer>\| \|\|<Left>
augroup END
"}}}

" ----------------------------------------
" Searching {{{
set hlsearch
set ignorecase
set incsearch
set smartcase
set wrapscan

nnoremap <silent> n nvv
nnoremap <silent> N Nvv
"}}}

" ----------------------------------------
" Moving {{{
set whichwrap=b,s,h,l,~,<,>,[,]
set virtualedit+=block

inoremap <silent><C-A> <End>
inoremap <silent><C-L> <Right>
inoremap jj <Esc>
nnoremap $ g_
xnoremap $ g_
nnoremap <silent><Down> gj
nnoremap <silent><Up>   gk
nnoremap <silent>j gj
nnoremap <silent>k gk
nnoremap <silent>gj j
nnoremap <silent>gk k
nnoremap <silent>Y y$

let g:alpaca_window_default_filetype = 'ruby'
let g:alpaca_window_max_height = winheight(0)
let g:alpaca_window_max_width = winwidth(0)
nmap <silent><C-W>n <Plug>(alpaca_window_smart_new)

augroup MyAutoCmd
  autocmd BufReadPost * if line("'\"") > 0 && line("'\"") <= line('$') | execute "normal g`\"" | endif
  autocmd FileType qf wincmd J
  autocmd FileType qf call alpaca_window#set_smart_close()
augroup END
"}}}

" ----------------------------------------
" Tab {{{
nmap [tag_or_tab] <Nop>
nmap t [tag_or_tab]
nmap <silent>[tag_or_tab]c      <Plug>(alpaca_window_tabnew)
nmap <silent>[tag_or_tab]w      <Plug>(alpaca_window_move_buffer_into_last_tab)
nnoremap <silent>[tag_or_tab]n  :tabnext<CR>
nnoremap <silent>[tag_or_tab]p  :tabprevious<CR>
nnoremap <silent>[tag_or_tab]x  :tabclose<CR>
nnoremap <silent>[tag_or_tab]o  <C-W>T

for i in range(0, 9)
  execute 'nnoremap <silent>[tag_or_tab]'.i.'  :tabnext '.i'<CR>'
  execute 'nnoremap <silent>[tag_or_tab]m'.i.'  :tabmove '.i'<CR>'
endfor
unlet i
"}}}

" ----------------------------------------
" Encoding {{{
set ambiwidth=double
set encoding=utf-8
set fileencodings=utf-8,sjis,shift-jis,euc-jp,utf-16,ascii,ucs-bom,cp932,iso-2022-jp
set fileformat=unix
set fileformats=unix,dos,mac
set suffixes=.bak,~,.swp,.o,.info,.aux,.log,.dvi,.bbl,.blg,.brf,.cb,.ind,.idx,.ilg,.inx,.out,.toc
set termencoding=utf8

command! -bang -bar -complete=file -nargs=? Cp932 edit<bang> ++enc=cp932 <args>
command! -bang -bar -complete=file -nargs=? Euc edit<bang> ++enc=euc-jp <args>
command! -bang -bar -complete=file -nargs=? Iso2022jp edit<bang> ++enc=iso-2022-jp <args>
command! -bang -bar -complete=file -nargs=? Jis  Iso2022jp<bang> <args>
command! -bang -bar -complete=file -nargs=? Sjis  Cp932<bang> <args>
command! -bang -bar -complete=file -nargs=? Unicode Utf16<bang> <args>
command! -bang -bar -complete=file -nargs=? Utf16 edit<bang> ++enc=ucs-2le <args>
command! -bang -bar -complete=file -nargs=? Utf16be edit<bang> ++enc=ucs-2 <args>
command! -bang -bar -complete=file -nargs=? Utf8 edit<bang> ++enc=utf-8 <args>
" }}}

" ----------------------------------------
" Indent {{{
set autoindent
set expandtab
set smartindent
set smarttab
set softtabstop=2
set shiftwidth=2
set tabstop=2
filetype indent on
"}}}

" ----------------------------------------
" Display {{{
" set noequalalways
" set cursorline
" set titlelen=95
set termguicolors
set pumheight=20
set breakat=\\;:,!?
set cdpath+=~
set cmdheight=2
set equalalways
set laststatus=2
set lazyredraw
set browsedir=buffer
set list
set listchars=tab:␣.,trail:›,extends:>,precedes:<
set fillchars=stl:\ ,stlnc:\ ,fold:\ ,diff:-
set matchpairs+=<:>
set number
set scrolloff=8
set noshowcmd
set showfulltag
set showmatch
set showtabline=1
set spelllang=en,cjk
set nospell
set t_Co=256
set title
set ttyfast
set shortmess=aTI

set nofoldenable
set foldmethod=marker
set foldlevelstart=0
set foldminlines=2
set foldnestmax=2

highlight ColorColumn guibg=#012345

augroup MyAutoCmd
  autocmd BufEnter * set conceallevel=2 concealcursor=incv colorcolumn=81
augroup END

colorscheme iceberg
filetype plugin on
syntax on
"}}}

" ----------------------------------------
" Tags {{{
set tags=./tags

nnoremap [tag_or_tab]t  <C-]>
nnoremap [tag_or_tab]h  :<C-u>pop<CR>
nnoremap [tag_or_tab]l  :<C-u>tag<CR>
nnoremap [tag_or_tab]j  :<C-u>tprevious<CR>
nnoremap [tag_or_tab]k  :<C-u>tags<CR>
nnoremap [tag_or_tab]s  :<C-u>tselect<CR>

if dein#is_sourced('alpaca_tags')
  augroup AlpacaTags
    autocmd!
    autocmd BufWritePost Gemfile AlpacaTagsBundle
    autocmd BufRead,BufNew * AlpacaTagsSet
    autocmd BufWritePost * AlpacaTagsUpdate
  augroup END
endif
"}}}

" ----------------------------------------
" Dictionary {{{
function! s:set_dictionary_automatically() "{{{
  let filetype = &filetype

  if empty(filetype) || count(g:my.ft.ignore_patterns, filetype) > 0
    return
  endif

  let dict_name = split(filetype, '\.')

  setl dictionary=

  let filetypes = []
  for ft in dict_name
    call add(filetypes, ft)

    let dict_path = '~/.vim/dict/'. join(filetypes, '.') .'.dict'
    execute 'setl dictionary+=' . expand(dict_path)

    if exists(':NeoSnippetSource')
      let snippet_path = expand('~/.vim/snippet/'. join(filetypes, '.') .'.snip')
      if filereadable(snippet_path)
        execute 'NeoSnippetSource '. snippet_path
      endif
    endif
  endfor
endfunc"}}}

augroup MyAutoCmd
  autocmd FileType * call <SID>set_dictionary_automatically()
augroup END
"}}}

" ----------------------------------------
" History {{{
set complete=.,w,b,u,U,s,i,d,t
set completeopt=menu,menuone
set history=1000
set infercase
set wildchar=<tab>
set wildmenu
set showfulltag
set wildoptions=tagfile
set wildmode=longest:full,full

" Complement command in command-line like zsh
cnoremap <C-P> <UP>
cnoremap <C-N> <Down>
"}}}

" ----------------------------------------
" Plugin settings {{{
nnoremap [plug] <Nop>
nmap <C-H> [plug]

" ------------------------------------
nmap cs  <Plug>Csurround
nmap ds  <Plug>Dsurround
nmap ySS <Plug>YSsurround
nmap ySs <Plug>YSsurround
nmap ys  <Plug>Ysurround
nmap yss <Plug>Yssurround

xmap S   <Plug>VSurround
xmap gS  <Plug>VgSurround
xmap s   <Plug>VSurround

" ------------------------------------
nnoremap <Space>t :Vista<CR>

" ------------------------------------
" open-browser
nmap ,o <Plug>(openbrowser-open)
xmap ,o <Plug>(openbrowser-open)
nnoremap ,g :<C-u>OpenBrowserSearch<Space><C-R><C-W><CR>

" ------------------------------------
nnoremap <silent>,r :QuickRun<CR>
nnoremap <silent>,R :autocmd BufWritePost <buffer> call quickrun#command('', len(range(0, line('$'))), 0, line('$'))<CR>

" ----------------------------------------
nnoremap <silent>gM :Gcommit --amend<CR>
nnoremap <silent>gb :Gblame<CR>
nnoremap <silent>gB :Gbrowse<CR>
nnoremap <silent>gm :Gcommit<CR>

" ----------------------------------------
nnoremap gA :<C-U>GitAdd<Space>
nnoremap <silent>ga :<C-U>GitAdd<CR>
nnoremap gp :<C-U>Git push<Space>
nnoremap gD :<C-U>GitDiff<Space>
nnoremap gDD :<C-U>GitDiff HEAD<CR>
nnoremap git :<C-U>Git<Space>

let g:git_bin = g:my.bin.git
let g:git_command_edit = 'vnew'
let g:git_no_default_mappings = 1

" ------------------------------------
map <silent> w <Plug>CamelCaseMotion_w
map <silent> b <Plug>CamelCaseMotion_b
sunmap w
sunmap b

nnoremap diw di,w
nnoremap dib di,b
nnoremap die di,e

nnoremap ciw ci,w
nnoremap cib ci,b
nnoremap cie ci,e

omap <silent>iw <Plug>CamelCaseMotion_iw
xmap <silent>iw <Plug>CamelCaseMotion_iw
omap <silent>ib <Plug>CamelCaseMotion_ib
xmap <silent>ib <Plug>CamelCaseMotion_ib
omap <silent>ie <Plug>CamelCaseMotion_ie
xmap <silent>ie <Plug>CamelCaseMotion_ie

" ------------------------------------
nnoremap <silent>,v  :<C-U>VimShellBufferDir -popup<CR>
nnoremap <silent>,V  :<C-U>VimShellCreate -popup<CR>

" ------------------------------------
function! s:new_memolist()
  let date = strftime('%Y-%m-%d')
  let input = input('Filename: ', date . '-') . '.md'
  tabnew `=g:my.dir.memolist . '/' . input`
endfunction

nnoremap <silent><Space>mn :call <SID>new_memolist()<CR>
nnoremap <silent><Space>ml :execute 'VimFilerExplorer ' . g:my.dir.memolist . ' -horizontal'<CR>

" ------------------------------------
function! s:define_rails_autocmd() "{{{
  let s:rails_snippets = {
        \ '.view*':                 ['ruby.rails.view'],
        \ '.view.haml':             ['haml.rails.view'],
        \ '.view.erb':              ['eruby.rails.view'],
        \ '.model*':                 ['ruby.rails.model'],
        \ '.controller':            ['ruby.rails.controller'],
        \ '.db.migration':          ['ruby.rails.migrate'],
        \ '/config/environment.rb': ['ruby.rails.environment'],
        \ '/config/routes.rb':      ['ruby.rails.route'],
        \ '.fixtures.replacement':  ['ruby.factory_girl'],
        \ '.spec.controller':       ['ruby.rspec.controller'],
        \ '.spec.model':            ['ruby.rspec.model'],
        \ '.spec.helper':           ['ruby.rspec.helper'],
        \ '.spec.feature':          ['ruby.capybara'],
        \ '.spec.routing':          ['ruby.rspec.routing']
        \ }

  augroup MyRailsSetting
    autocmd!

    for [type_or_path, snippets] in items(s:rails_snippets)
      for snippet in snippets
        let snippet_file = expand('~/.vim/snippet/' . snippet . '.snip')
        if filereadable(snippet_file)
          let prefix = 'autocmd User Rails' . type_or_path
          execute prefix . ' NeoSnippetSource ' . snippet_file
          execute prefix . ' nnoremap <buffer><Space>e :<C-U>tabnew ' . snippet_file . '<CR>'
        endif
      endfor
    endfor
  augroup END
 endfunction"}}}

function! s:detect_rails()
  let root_dir = s:current_git()
  return !empty(root_dir) && filereadable(root_dir . '/config/application.rb')
endfunction

function! s:load_vim_rails() "{{{
  if exists('s:loaded_vim_rails')
    return
  endif

  if s:detect_rails()
    let s:loaded_vim_rails = 1
    let root_dir = s:current_git()

    autocmd! LoadVimRails
    augroup! LoadVimRails

    let b:rails_root = root_dir
    call dein#source('vim-rails')
    call s:define_rails_autocmd()

    silent doautocmd User BufEnterRails
    silent doautocmd User Rails

    augroup DetectVimRails
      autocmd!
      autocmd BufNew,BufNewFile *.rb silent doautocmd User Rails
    augroup END
  endif
endfunction"}}}

if s:on_init() && !exists('s:loaded_vim_rails')
  augroup LoadVimRails
    autocmd!
    autocmd VimEnter,BufNewFile,BufRead * call <SID>load_vim_rails()
  augroup END
endif

" ------------------------------------
nnoremap <silent>[unite]w  :Unite tweetvim -buffer-name=tweetvim -no-start-insert<CR>
nnoremap <silent>[tag_or_tab]v :TweetVimSay<CR>

" ------------------------------------
" vim-endwise
" ------------------------------------
let g:endwise_no_mappings = 1

" ------------------------------------
let g:vinarise_objdump_command = 'gobjdump'

" ------------------------------------
" Gundo.vim
" ------------------------------------
nnoremap <Space>u :GundoToggle<CR>

" ------------------------------------
" excitetranslate
" ------------------------------------
xnoremap <C-E> :<C-U>ExciteTranslate<CR>

" ------------------------------------
"  simple-javascript-indenter
let g:SimpleJsIndenter_BriefMode = 1
let g:SimpleJsIndenter_CaseIndentLevel = -1

" ------------------------------------
" t_comment
" ------------------------------------
if has('gui_running')
  let g:tcommentMaps = 0
  nmap <silent><C-_> :TComment<CR>
  xmap <silent><C-_> :TComment<CR>
  nmap <silent>_ <C-_>
  xmap <silent>_ <C-_>
  " nmap gc <Plug>(caw:i:toggle)
  " xmap gc <Plug>(caw:i:toggle)
else
  nmap <C-_> :TComment<CR>
  xmap <C-_> :TComment<CR>
  " nmap <C-_> <Plug>(caw:i:toggle)
  " xmap <C-_> <Plug>(caw:i:toggle)
endif

" ------------------------------------
" alpaca_remove_dust
" ------------------------------------
let g:remove_dust_enable = 1

if exists(':RemoveDust')
  augroup RemoveDust
    autocmd!
    autocmd BufWritePre * RemoveDust
    autocmd FileType ts,go,c,make RemoveDustDisable
  augroup END
endif

" ------------------------------------
" vim-niceblock
" ------------------------------------
xmap I  <Plug>(niceblock-I)
xmap A  <Plug>(niceblock-A)

" ------------------------------------
nnoremap ! :Switch<CR>

" ------------------------------------
" VimFiler "{{{
nnoremap <silent><Space>f  :call <SID>vim_filer_explorer_git()<CR>
nnoremap <silent>,,  :VimFilerCreate<CR>
"}}}

" ----------------------------------------
nnoremap <silent><Space>e   :NeoSnippetEdit -split<CR>
nnoremap <silent>so         :call <SID>open_neosnippet_sources()<CR>
function! s:open_neosnippet_sources() "{{{
  silent! execute 'Unite neosnippet/user neosnippet/runtime -no-start-insert -default-action=neosnippet_source -input=' . s:filetype()
endfunction"}}}

let g:neosnippet#enable_preview = 1
let g:neosnippet#snippets_directory = g:my.dir.snippets

augroup NeoSnippetAug
  autocmd!
  autocmd BufWritePre * if &modifiable && !&readonly
        \|   call neosnippet#commands#_clear_markers()
        \| endif
augroup END

" ----------------------------------------
" unite.vim"{{{
" keymappings
nmap [unite] <Nop>
nmap <C-J> [unite]

nnoremap <silent>[unite]j       :Unite neomru/file<CR>
nnoremap <silent>g/             :Unite line -hide-source-names -horizontal -start-insert -no-quit<CR>

function! s:parse_git_root_option(input) "{{{
  let reg_multi =  '\*\*/'
  let reg_bad_pattern =  '\v(\*\*[^/])'
  let reg_single = '[^/*]\{,1}\*[^*]'

  let input = a:input
  if input =~ reg_bad_pattern
    next
  elseif input =~ reg_multi
    let input = substitute(input, '.*/\([^/]\+\)$', '\1', 'g')
  elseif input =~ reg_single
    let input = '^' . input
  endif

  return input
endfunction"}}}

function! s:unite_git_root(...) "{{{
  let git_root = s:current_git()
  let root_path = s:complement_delimiter_of_directory(git_root)

  let argument = empty(a:000) ? '' : a:1
  let [args, context] = unite#helper#parse_options_args(argument)
  let full_path = get(unite#helper#get_source_names(args), 0, '')
  let wild_sep = split(full_path, '\*')
  let relative_path = get(wild_sep, 0, '')
  let absolute_path = root_path . relative_path

  let prefilter = len(wild_sep) > 1 ? '*' . join(wild_sep[1:-1], '*') : ''
  let prefilter = s:parse_git_root_option(prefilter)
  let context.source__prefilters = [prefilter]
  let context.source__absolute_path = absolute_path
  let context.source__project_root_path = git_root

  if isdirectory(absolute_path)
    lcd `=absolute_path`
    call unite#start([['file_rec', absolute_path]], context)
  elseif filereadable(absolute_path)
    edit `=absolute_path`
  else
    echomsg absolute_path . ' is not exists!'
  endif
endfunction"}}}

function! s:unite_git_complete(arg_lead, cmd_line, cursor_pos) "{{{
  let git_root = s:complement_delimiter_of_directory(s:current_git())
  let files = globpath(git_root, a:arg_lead . '*')
  let file_list = split(files, '\n')
  let file_list = map(file_list, 's:complement_delimiter_of_directory(v:val)')
  let file_list = map(file_list, "substitute(v:val, git_root, '', 'g')")

  return file_list
endfunction "}}}

command! -nargs=? -complete=customlist,s:unite_git_complete UniteGit call <SID>unite_git_root(<f-args>)

" vim-unite-giti
nnoremap <silent>gl :Unite giti/log -no-start-insert -horizontal<CR>
nnoremap <silent>gs :Unite giti/status -no-start-insert -horizontal<CR>
nnoremap <silent>gh :Unite giti/branch_all -no-start-insert<CR>
"}}}

nmap p <Plug>(yankround-p)
nmap P <Plug>(yankround-P)
nmap <C-P> <Plug>(yankround-prev)
nmap <C-N> <Plug>(yankround-next)
" let g:yankround_use_region_hl = 1

" ----------------------------------------
" Other plugin settings {{{
let g:git_aliases#author_name = g:my.info.github
"}}}

let g:switch_file_rules = {
      \ "vim": [['autoload/%\.vim', 'plugin/%\.vim']],
      \ "ruby": [['spec/requests/%_spec\.rb', 'app/controllers/%_controller\.rb'], ['spec/%_spec\.rb', 'app/%\.rb'], ['spec/%_spec\.rb', 'lib/%\.rb']],
      \ "typescript": [['%\.ts', '__tests__/%.test.ts']]
      \ }

nnoremap <silent><Space>a :call switch_file#next()<CR>
nnoremap <silent><Space>A :call switch_file#prev()<CR>

" ----------------------------------------

" git-messenger.vim
nnoremap <silent>P :GitMessenger<CR>

set hidden
"}}}

" ----------------------------------------
function! s:update_ruby_ctags() "{{{
  call alpaca#system_bg('rbenv ctags')
  call alpaca#system_bg('gem ctags')
endfunction"}}}
command! UpdateRubyTags call <SID>update_ruby_ctags()

" ----------------------------------------
" Clean memory {{{
function! s:clear_memory() "{{{
  if exists(':NeoCompleteClean')
    NeoCompleteClean
    NeoCompleteDisable
    NeoCompleteEnable
  endif

  if exists(':TagsCleanCache')
    TagsCleanCache
    set tags=
  endif

  execute '1,' . bufnr('$') . 'bwipeout'
endfunction "}}}

command! UnwatchBuffer setlocal buftype=nofile nobuflisted noswapfile bufhidden=hide
command! Clean call <SID>clear_memory()
command! CleanSwap call alpaca#system_bg('rm -rf ' . g:my.dir.swap_dir . '/*')
command! CleanAll call alpaca#system_bg('rm -rf ' . g:my.dir.trash_dir . '*')
"}}}

" For Gem
" Gem {{{
function! s:get_all_gem_paths() "{{{
  if exists('s:all_gem_paths')
    return s:all_gem_paths
  endif

  let gem_env = system('rbenv exec gem env')
  let gem_path = matchstr(gem_env, '\(\s*- INSTALLATION DIRECTORY:\s\)\@<=[^\n]*')
  let gem_path .= '/gems/'
  let s:all_gem_paths = split(globpath(gem_path, '*/lib'), '\n')

  return s:all_gem_paths
endfunction"}}}

function! s:get_gem_paths() " {{{
  if !exists('s:gem_path')
    let s:gem_path = {}
  endif

  let project_dir = s:get_vital().import('Prelude').path2project_directory(getcwd())

  if !has_key(s:gem_path, project_dir)
    try
      let paths = map(split(result, '\n'), 'v:val . "/lib"')
      call filter(paths, 'isdirectory(v:val)')
      let s:gem_path[project_dir] = paths
    catch /.*/
      let s:gem_path[project_dir] = s:get_all_gem_paths()
    endtry
  endif

  return s:gem_path[project_dir]
endfunction " }}}

function! s:build_path(path) "{{{
  let path = join(map(copy(a:path), 'v:val ==# "." ? "" : v:val'), ',')
  if &g:path !~# '\v^\.%(,/%(usr|emx)/include)=,,$'
    let path = substitute(&g:path,',,$',',','') . ',' . path
  endif

  return path
endfunction"}}}

function! s:set_gem_paths() "{{{
  let gem_paths = join(s:get_gem_paths(), ',')

  if stridx(&l:path, gem_paths) == -1
    execute 'setlocal path+=' . gem_paths
  endif
endfunction"}}}

function! s:get_vital() "{{{
  if !exists('s:V')
    if dein#is_sourced('neocomplete.vim')
      let s:V = neocomplete#util#get_vital()
    elseif dein#is_sourced('unite.vim')
      let s:V = unite#util#get_vital()
    elseif dein#is_sourced('vital.vim')
      let s:V = vital#vital#new()
    else
      call dein#source('vital.vim')
      let s:V = vital#vital#new()
    endif
  endif

  return s:V
endfunction"}}}

function! s:load_gem_paths() "{{{
  if !empty(s:get_vital())
    call s:set_gem_paths()
  endif
endfunction"}}}

command! LoadGemPath call s:load_gem_paths()
"}}}

function! s:open_file_rec() " {{{
  autocmd! OpenFileRec

  if argc() == 0
    VimFiler
  endif
endfunction

if has('vim_starting')
  augroup OpenFileRec
    autocmd!
    autocmd VimEnter * call s:open_file_rec()
  augroup END
endif
" }}}

function! s:copy_current_path() "{{{
  let @* = expand('%:p')
  echo 'Copy current path: ' . @*
endfunction "}}}

function! s:copy_current_path_from_root() "{{{
  if empty(s:current_git())
    return
  else
    call s:copy_current_path()
    let @* = substitute(@*, s:current_git() . '/', '', 'g')
    echo 'Copy current path: ' . @*
  endif
endfunction "}}}

augroup MyAutoCmd
  autocmd WinEnter <buffer> checktime
augroup END

nmap cp :call <SID>copy_current_path()<CR>
nmap cpr :call <SID>copy_current_path_from_root()<CR>

command! OpenLocale call rails_locale#open_locale_file_from_path(expand('%:p'))

" Installation check.
let s:loaded_vimrc = 1
set secure
